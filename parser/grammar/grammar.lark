start 		: (figure_stmt)*
figure_stmt	:"fig" id "{" stmt_list "}"	-> figure_stmt

stmt_list 	: (stmt)*
stmt 		: size_stmt
       		| range_stmt
       		| function_stmt
       		| plot_stmt 
       		| contour_grid
       		| contour_stmt
       		| print_stmt




size_stmt : "size" "<-" INT [attr_list] -> size_row_stmt
	| "size" "<-" INT "," INT [attr_list] -> size_row_col_stmt
	| "size" "<-" "(" INT "," INT ")" [attr_list] -> size_row_col_stmt


sum_expr: prod_expr -> expr
		| sum_expr "+" prod_expr -> sum_expr
		| sum_expr "-" prod_expr -> sub_expr
prod_expr: exp_expr -> expr
		| prod_expr "*" exp_expr -> prd_expr
		| prod_expr "/" exp_expr -> div_expr
		| prod_expr "%" exp_expr -> mod_expr
exp_expr: atom_expr -> expr
		| exp_expr "^" atom_expr -> exp_expr
atom_expr	: REAL 				-> expr_real
			| CONST 			-> expr_const
			| "-" atom_expr		-> expr_negation
			| "(" sum_expr ")"	-> nested_expr
			| id 				-> expr_id



range_stmt 	: id "<-" points 	-> range_stmt
			| id "<-" range		-> range_stmt

points 	: "[" REAL ("," REAL)* "]"	-> real_points_list
		| "[" ESC_STR ("," ESC_STR)* "]" -> str_points_list

range : "(" sum_expr "," sum_expr ")" ("step" sum_expr)? -> range_expr


function_stmt 	: id ":=" func_expr 	-> func_stmt
func_expr : sum_expr 	-> expr
	| id "(" func_expr ")" 		-> func_call_unary
	| id "(" func_expr "," func_expr ")" -> func_call_binary


plot_stmt : "plot" "(" id "," id ("," attr_list)? ")"	-> plt_stmt

contour_grid: id "," id "<-" "contour_grid" "(" REAL "," REAL "," REAL "," REAL ")" -> contour_grid
contour_stmt: "contour" "(" id "," id "," func_expr ("," attr_list)? ")" -> contour_stmt



print_stmt	:	"print" "(" id ("," id)* ")" -> print_stmt

attr_list 		: "{" attr ("," attr)* "}"
attr 			: id "=" ATTR_VALUE		-> attribute
id 		        : ID 		-> identifier




ATTR_VALUE 		: ESC_STR | REAL | BOOLEAN
BOOLEAN			: "true" | "false"
COMMENT			: 	"#" /.*/
CONST 			: UCASE_LETTER+
LCASE_LETTER    : "a".."z"
UCASE_LETTER    : "A".."Z"
LETTER          : UCASE_LETTER | LCASE_LETTER
ID 				: ("_"|LETTER) ("_"|LETTER|DIGIT)*
DIGIT           : "0".."9"
INT             : DIGIT+
SINT      		: ["+"|"-"] DIGIT+
DECIMAL         : INT "." INT? | "." INT
_EXP            : ("e"|"E") SINT
FLOAT           : INT _EXP | DECIMAL _EXP?
NUMBER          : FLOAT | INT
REAL		    : ["+"|"-"] NUMBER 
ESC_STR 		: ("\"" _STRING_ESC_INNER "\"")
				| ("'" _STRING_ESC_INNER "'")
_STRING_INNER       : /.*?/
_STRING_ESC_INNER   : _STRING_INNER /(?<!\\)(\\\\)*?/
WS: /[ \t\f\r\n]/+

%ignore WS
%ignore COMMENT
